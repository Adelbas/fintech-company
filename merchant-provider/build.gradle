import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
    alias(libs.plugins.spring)
    alias(libs.plugins.open.api)
}

dependencies {
    implementation libs.javax.annotation

    implementation libs.spring.data.jpa
    implementation libs.postgresql.driver
    implementation libs.spring.validation

    implementation libs.open.api.springdoc
    implementation libs.open.api.generator

    implementation libs.mapstruct
    annotationProcessor libs.mapstruct.processor
}

tasks.register('generateServerCode', GenerateTask) {
    generatorName.set("spring")
    inputSpecRootDirectory.set("$rootDir/merchant-provider/src/main/resources/static/server")
    outputDir.set("$buildDir/generated/openapi/server")
    apiPackage.set("com.academy.fintech.mp.server.api")
    invokerPackage.set("com.academy.fintech.mp.server.invoker")
    modelPackage.set("com.academy.fintech.mp.server.model")
    setGenerateModelTests(Boolean.FALSE)
    setGenerateApiTests(Boolean.FALSE)
    configOptions.set([
            library                             : "spring-boot",
            useOptional                         : "false",
            openApiNullable                     : "false",
            interfaceOnly                       : "true",
            useJakartaEe                        : "true",
            sourceFolder                        : "",
            generatedConstructorWithRequiredArgs: "false",
            additionalModelTypeAnnotations      : "@lombok.NoArgsConstructor\n" +
                    "@lombok.AllArgsConstructor\n@lombok.Builder(toBuilder = true)\n" +
                    "@com.fasterxml.jackson.annotation.JsonIgnoreProperties(ignoreUnknown=true)\n" +
                    "@com.fasterxml.jackson.annotation.JsonInclude(com.fasterxml.jackson.annotation.JsonInclude.Include.NON_NULL)",
            useTags                             : "true"
    ])
}

tasks.register('generateClientCode', GenerateTask) {
    generatorName.set("java")
    inputSpecRootDirectory.set("$rootDir/merchant-provider/src/main/resources/static/client")
    outputDir.set( "$buildDir/generated/openapi/client")
    apiPackage.set("com.academy.fintech.mp.client.api")
    invokerPackage.set("com.academy.fintech.mp.client.invoker")
    modelPackage.set("com.academy.fintech.mp.client.model")
    setGenerateModelTests(Boolean.FALSE)
    setGenerateApiTests(Boolean.FALSE)
    configOptions.set([
            library                             : "resttemplate",
            useOptional                         : "false",
            openApiNullable                     : "false",
            interfaceOnly                       : "true",
            useJakartaEe                        : "true",
            sourceFolder                        : "",
            generatedConstructorWithRequiredArgs: "false",
            additionalModelTypeAnnotations      :"@lombok.AllArgsConstructor\n@lombok.Builder(toBuilder = true)\n" +
                    "@com.fasterxml.jackson.annotation.JsonIgnoreProperties(ignoreUnknown=true)\n" +
                    "@com.fasterxml.jackson.annotation.JsonInclude(com.fasterxml.jackson.annotation.JsonInclude.Include.NON_NULL)",
            useTags                             : "true"
    ])
}

compileJava.dependsOn tasks.generateServerCode, tasks.generateClientCode

sourceSets {
    main {
        java {
            srcDirs += "$buildDir/generated"
        }
    }
}